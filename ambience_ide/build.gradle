plugins {
    id 'java'
    id "war"
    id "org.teavm" version "0.13.0"
    id "org.gretty" version "4.0.3"
}

evaluationDependsOn(':engine')

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

repositories {
    mavenCentral()
}

dependencies {
    implementation teavm.libs.jsoApis
}

gretty {
    httpPort = 8080
    contextPath = '/'
}

teavm {
    all {
        mainClass = "me.molybdenum.ambience_ide.Test"
    }
    js {
        addedToWebApp = true

        // this is also optional, default value is <project name>.js
        targetFileName = "ambience_ide.js"
    }
    wasmGC {
        addedToWebApp = true

        // this is also optional, default value is <project name>.wasm
        targetFileName = "ambience_ide.wasm"
    }
}

def generatedDir = "src/main/java/me/molybdenum/ambience_ide"

tasks.register('generateBuildConfig') {
    doLast {
        def outputDir = file(generatedDir)
        outputDir.mkdirs()
        def file = new File(outputDir, "BuildConfig.java")
        file.text = """package me.molybdenum.ambience_ide;
 
public final class BuildConfig {
    public static final String APP_VERSION = "${version}";
}
"""
    }
}

compileJava.dependsOn "generateBuildConfig"

tasks.withType(JavaCompile).configureEach {
    source(project(':engine').sourceSets.main.allSource)
}
