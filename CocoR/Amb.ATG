/* This file contains the Token specification and Grammar which COCO/R uses to construct the scanner
and parser. COCO/R uses Extend Bacus-Naur form (EBNF) for the grammar, as well as its own series of commands
which will be explained when met first in context.  */

// The scanner/lexer and parser files are created by inserting generated code (based on this .ATG file)
// into the "Scanner.frame" and "Parser.frame" files located alongside the Dims.ATG file.

/* The EBNF uses the following extensions: */
/* [] Brackets mean that the string is written zero or one times.  */
/* {} Curly brackets mean that the string is repeated zero or more times  */
/* () is a grouping used alongside |. The parenthesis contains a series of strings separated by |. This means that exactly one of the strings is chosen. */

/* Imports here are inserted at the top of the generated lexer and parser. */
import me.molybdenum.ambience_mini.engine.loader.abstract_syntax.expression.*;
import me.molybdenum.ambience_mini.engine.loader.abstract_syntax.config.*;
import me.molybdenum.ambience_mini.engine.loader.abstract_syntax.schedule.*;
import me.molybdenum.ambience_mini.engine.loader.abstract_syntax.playlist.*;
import java.util.ArrayList;

/* The required start of a Coco/R grammar file. "Program" specifies the starting symbol of the grammar. */
COMPILER File

/* Code here is injected at the start of the parser class. */
    public Config mainNode = null; // This contains the AST generated by calling parser.Parse().

    public boolean hasErrors() {
        return errors.count > 0;
    }

    private static String removeFirstAndLast(String str) {
        return str.substring(1, str.length() - 1);
    }

/*------------------------------------------------------------------------*/
/* The following section contains the token specification of Dims.*/
CHARACTERS
  letter = "ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz" .
  digit = "0123456789" .
  lf  = '\n' .
  stringChar = ANY - lf - '\r' - '"'.

TOKENS
  IDENT  = ('_' | letter) {letter | digit | '_'}  .
  INT    = ['-'|'+'] digit {digit}  .
  FLOAT  = ['-'|'+'] digit {digit} '.' digit {digit}  .
  STRING = '"' {stringChar} '"' .

COMMENTS FROM "/*" TO "*/" NESTED  /* Nested means: "There can be comments inside comments" */
COMMENTS FROM "//" TO lf

IGNORE '\r' + '\n' + '\t' /* These characters are ignored by the lexer */


/*------------------------------------------------------------------------*/
/* The rest of the file contains the grammar for Dims.  */
PRODUCTIONS

File = Conf<out mainNode> .

Conf<out Config conf>                (. conf = null; .)
    = "playlist" IDENT               (. IdentP ident = new IdentP(t.val); .)
      '=' Play<out Playlist play> ';'
      Conf<out Config inner>         (. conf = new PlaylistDecl(ident, play, inner); .)
    | Shed<out Schedule schedule>    (. conf = new ScheduleDecl(schedule); .)
.

/*------------------------------------------------------------------------*/
Play<out Playlist play>
     = PlayTerm<out play>
         { "++" PlayTerm<out Playlist play2> (. play = new Concat(play, play2); .)
         }
.

PlayTerm<out Playlist play>               (. play = new Nil(); FloatLit gain = null; .)
    = '['
        [ PlayTerm<out Playlist load>     (. play = load;      .)
          { ',' PlayTerm<out load>        (. play = new Concat(play, load); .)
          }
        ]
      ']'
    | IDENT                         (. play = new IdentP(t.val); .)
    | "NIL"
    | STRING                    (. StringLit file = new StringLit(removeFirstAndLast(t.val)); .)
            [ '<' (FLOAT|INT)   (. gain = new FloatLit(Float.parseFloat(t.val));            .)
              '>'
            ]                   (. play = new Load(file, gain);                           .)
.

/*------------------------------------------------------------------------*/
Shed<out Schedule schedule>                  (. schedule = null; boolean instant = false; .)
    = "play" ["instant"                      (. instant = true; .)
      ] Play<out Playlist pl> ';'            (. schedule = new Play(pl, instant); .)
//    | "print" Expr<out Expr expr>            (. ArrayList<Shed> exprs = new ArrayList<>(); exprs.add(expr); .)
//      { ',' Expr<out expr>                   (. exprs.add(expr); .)
//      } ';'                                  (. shed = new Print(exprs); .)
    | "begin"                                (. ArrayList<Schedule> items = new ArrayList<>();  .)
      { Shed<out Schedule schedule2>         (. items.add(schedule2); .)
      }
      "end"                                  (. schedule = new Block(items); .)
    | "when" '(' Expr<out Expr expr>
      ')' Shed<out Schedule schedule2>         (. schedule = new When(expr, schedule2); .)
    | "default" Shed<out Schedule schedule2>   (. schedule = new When(new BoolLit(true), schedule2); .)
    | "interrupt" Shed<out Schedule schedule2> (. schedule = new Interrupt(schedule2); .)
.

/*------------------------------------------------------------------------*/
Expr<out Expr expr>              (. BinaryOperators op = BinaryOperators.AND; .)
    = ExprEq<out expr>
    { ( "&&"                     (. op = BinaryOperators.AND; .)
      | "||"                     (. op = BinaryOperators.OR; .)
      ) ExprEq<out Expr expr2>   (. expr = new BinaryOp(op, expr, expr2); .)
    }
.

ExprEq<out Expr expr>            (. BinaryOperators op = BinaryOperators.EQ; .)
    = ExprRel<out expr>
    { ( "=="                     (. op = BinaryOperators.EQ; .)
      | "~~"                     (. op = BinaryOperators.APP_EQ; .)
      ) ExprRel<out Expr expr2> (. expr = new BinaryOp(op, expr, expr2); .)
    }
.

ExprRel<out Expr expr>            (. BinaryOperators op = BinaryOperators.EQ; .)
    = ExprTerm<out expr>
    { ( "<"                     (. op = BinaryOperators.LT; .)
      ) ExprTerm<out Expr expr2> (. expr = new BinaryOp(op, expr, expr2); .)
    }
.

ExprTerm<out Expr expr> (. expr = null; Quantifiers quantifier = Quantifiers.ANY; .)
    = IDENT             (. expr = new IdentE(t.val);                      .)
    | INT               (. expr = new IntLit(Integer.parseInt(t.val));      .)
    | FLOAT             (. expr = new FloatLit(Float.parseFloat(t.val));    .)
    | STRING            (. expr = new StringLit(removeFirstAndLast(t.val)); .)
    | "true"            (. expr = new BoolLit(true);                        .)
    | "false"           (. expr = new BoolLit(false);                       .)
    | '@' IDENT         (. expr = new GetEvent(new IdentE(t.val));              .)
    | '$' IDENT         (. expr = new GetProperty(new IdentE(t.val));             .)
    | ("any" | "all"    (. quantifier = Quantifiers.ALL; .)
      ) IDENT           (. String identifier = t.val; .)
      "in" Expr<out Expr list> "where" Expr<out Expr cond> "end"
                        (. expr = new QuantifierOp(quantifier, identifier, list, cond); .)
    | '(' Expr<out expr> ')'
.

END File. // Must refer to the same non-terminal as "COMPILER" at the top.